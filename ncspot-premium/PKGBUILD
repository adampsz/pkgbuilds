
pkgname=ncspot-premium
pkgver=0.11.0
pkgrel=1
pkgdesc="Cross-platform ncurses Spotify client written in Rust, inspired by ncmpc and the likes."
arch=('x86_64' 'aarch64' 'armv7h')
url="https://github.com/hrkfdn/ncspot"
license=('BSD')
depends=('ncurses' 'openssl' 'libpulse' 'libxcb' 'ncurses' 'dbus')
makedepends=('rust' 'cargo' 'git' 'alsa-lib' 'python' 'pkgconf')
provides=(ncspot)
conflicts=(ncspot)
source=("ncspot-${pkgver}.zip::https://github.com/hrkfdn/ncspot/archive/v${pkgver}.zip"
        'librespot-0.4.2.zip::https://github.com/librespot-org/librespot/archive/v0.4.2.zip'
        'ncspot.patch'
        'librespot.patch')
sha512sums=('5281a1b2d60ad495f0c416cae7548ce9847afbaea8859e2a332af955b0dc53e2818825a75e23a76659a7b5a54195b24c1dfe482cef2de265ae6b1dfa45bcf4b6'
            'f3cd95cf63eb51c20f13a493443bdba0d3264789d349945ce1f04d93b451c07909b74d203d7db8badda7e951a5f6c208a42fc7b1d6f25b15e5597be450e6cb16'
            'e7845a99865a89218eafef184bedca416b81a5149fbb4a37b8888c3333dfb290c6d8d8b03d48e110a5cbab7795256673588cefb6e265b3b420d93a82430704df'
            '2f1bdd70a90a700e467a211e28827f5ea5f7248cc17b0312ba09cb3f08eceeb5d6f91758bace29dfd8149898a14b52fa027e350f814d003f0ae1acc4df7c78eb')

prepare() {
  cd "${srcdir}/librespot-0.4.2"
  patch --forward --strip=1 --input="${srcdir}/librespot.patch"

  cd "${srcdir}/ncspot-${pkgver}"
  patch --forward --strip=1 --input="${srcdir}/ncspot.patch"
  cargo fetch
}

build() {
  cd "${srcdir}/ncspot-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${srcdir}/ncspot-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${srcdir}/ncspot-${pkgver}"
  #cargo install --root "${pkgdir}/usr" --path "${srcdir}/ncspot-${pkgver}"
  install -Dm 755 "target/release/ncspot" "${pkgdir}/usr/bin/ncspot"
  install -Dm 644 "misc/ncspot.desktop" "${pkgdir}/usr/share/applications/ncspot.desktop"
  install -Dm 644 "images/logo.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/ncspot.svg"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/ncspot/LICENSE"
}

