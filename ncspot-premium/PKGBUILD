
pkgname=ncspot-premium
pkgver=0.10.0
pkgrel=2
pkgdesc="Cross-platform ncurses Spotify client written in Rust, inspired by ncmpc and the likes."
arch=('x86_64' 'aarch64' 'armv7h')
url="https://github.com/hrkfdn/ncspot"
license=('BSD')
depends=('ncurses' 'openssl' 'libpulse' 'libxcb' 'ncurses' 'dbus')
makedepends=('rust' 'cargo' 'git' 'alsa-lib' 'python' 'pkgconf')
provides=(ncspot)
conflicts=(ncspot)
source=("https://github.com/hrkfdn/ncspot/archive/v${pkgver}.zip"
        'https://github.com/librespot-org/librespot/archive/v0.4.1.zip'
        'ncspot.patch'
        'librespot.patch')
sha512sums=('4d71e56f38385f529cb36fb4c589468a0cd5b4504bb400e0b92efdd7dfb36e6d05891ce6f254c0ca4ad8c7c02dce00be5dfac46cd690f8a9382f56f296bec6a2'
            'cfe6f2cade7315afb46defc24a007b09fe5d69c00f2547ce44b931fb114745f49687205bd3e7d473212b0db761ec790a43b8c39a205064a725f7b5c9c5215907'
            '3fd97b82547983f4e9708f3aba6c404bda47e74aacc4f1353bf120abf1b0afa7811646c3f915bc6fd3533a8af548a7dfbeffd10653f1adf180700e7247a49d1c'
            '2f1bdd70a90a700e467a211e28827f5ea5f7248cc17b0312ba09cb3f08eceeb5d6f91758bace29dfd8149898a14b52fa027e350f814d003f0ae1acc4df7c78eb')

prepare() {
  cd "${srcdir}/librespot-0.4.1"
  patch --forward --strip=1 --input="${srcdir}/librespot.patch"

  cd "${srcdir}/ncspot-${pkgver}"
  patch --forward --strip=1 --input="${srcdir}/ncspot.patch"
  cargo fetch
}

build() {
  cd "${srcdir}/ncspot-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${srcdir}/ncspot-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${srcdir}/ncspot-${pkgver}"
  #cargo install --root "${pkgdir}/usr" --path "${srcdir}/ncspot-${pkgver}"
  install -Dm 755 "target/release/ncspot" "${pkgdir}/usr/bin/ncspot"
  install -Dm 644 "misc/ncspot.desktop" "${pkgdir}/usr/share/applications/ncspot.desktop"
  install -Dm 644 "images/logo.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/ncspot.svg"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/ncspot/LICENSE"
}

