
pkgname=ncspot-premium
pkgver=0.10.1
pkgrel=1
pkgdesc="Cross-platform ncurses Spotify client written in Rust, inspired by ncmpc and the likes."
arch=('x86_64' 'aarch64' 'armv7h')
url="https://github.com/hrkfdn/ncspot"
license=('BSD')
depends=('ncurses' 'openssl' 'libpulse' 'libxcb' 'ncurses' 'dbus')
makedepends=('rust' 'cargo' 'git' 'alsa-lib' 'python' 'pkgconf')
provides=(ncspot)
conflicts=(ncspot)
source=("https://github.com/hrkfdn/ncspot/archive/v${pkgver}.zip"
        'https://github.com/librespot-org/librespot/archive/v0.4.2.zip'
        'ncspot.patch'
        'librespot.patch')
sha512sums=('187bb369fa1a4ede52ae04da7a941c9479ed5fe933cd3a51030cb353d38d5ce8fba0b016f1faa81aba7423095aea5c508b5a4798b78b9b9b2b6a68f6749d4d11'
            'f3cd95cf63eb51c20f13a493443bdba0d3264789d349945ce1f04d93b451c07909b74d203d7db8badda7e951a5f6c208a42fc7b1d6f25b15e5597be450e6cb16'
            '4996b440e1856aedc6163d7a07c6c61650ebeae13b275c2461112b267c1371242367eae994fed165ac395c146b5294fb34bc5510e82e60eb58ab1d6ea7c461d1'
            '2f1bdd70a90a700e467a211e28827f5ea5f7248cc17b0312ba09cb3f08eceeb5d6f91758bace29dfd8149898a14b52fa027e350f814d003f0ae1acc4df7c78eb')

prepare() {
  cd "${srcdir}/librespot-0.4.2"
  patch --forward --strip=1 --input="${srcdir}/librespot.patch"

  cd "${srcdir}/ncspot-${pkgver}"
  patch --forward --strip=1 --input="${srcdir}/ncspot.patch"
  cargo fetch
}

build() {
  cd "${srcdir}/ncspot-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${srcdir}/ncspot-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${srcdir}/ncspot-${pkgver}"
  #cargo install --root "${pkgdir}/usr" --path "${srcdir}/ncspot-${pkgver}"
  install -Dm 755 "target/release/ncspot" "${pkgdir}/usr/bin/ncspot"
  install -Dm 644 "misc/ncspot.desktop" "${pkgdir}/usr/share/applications/ncspot.desktop"
  install -Dm 644 "images/logo.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/ncspot.svg"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/ncspot/LICENSE"
}

