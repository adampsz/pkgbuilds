# Maintainer:
# shellcheck shell=ksh disable=SC2034

_commit=9ff9f8199ccf9d55fb1cf066cbbc791101894ab0

pkgname=gentoo-bashrc
pkgver=2024.06.22.9ff9f8199cc
pkgrel=1
pkgdesc="This is Gentoo's BASHRC file, which has very nice color schemes."
arch=('any')
url="https://gitweb.gentoo.org/repo/gentoo.git/tree/app-shells/bash/files"
license=('GPL')
optdepends=('bash: Shell needed to make use of this package')
source=("bashrc::https://gitweb.gentoo.org/repo/gentoo.git/plain/app-shells/bash/files/bashrc-r1?id=$_commit"
        "10-gentoo-color.bash::https://gitweb.gentoo.org/repo/gentoo.git/plain/app-shells/bash/files/bashrc.d/10-gentoo-color.bash?id=$_commit"
        "10-gentoo-title.bash::https://gitweb.gentoo.org/repo/gentoo.git/plain/app-shells/bash/files/bashrc.d/10-gentoo-title.bash?id=$_commit"
        "feed.xml::https://gitweb.gentoo.org/repo/gentoo.git/atom/app-shells/bash?h=$_commit")
sha256sums=('a19a62c8e4a3e2d6622ac874bdd1287bd470802f902275eb6680bc62e79d49f4'
            '579af0d935be990abc00fc54eeb7439d64b49ca4a1636e16eff950a91a8c76f2'
            '9aab8042a04a3bb4118341e18a42e8994e3d913d7bc4014a8dbf0fa1eccf1abf'
            'c42aa93125b171231a6bf896d36ea88f4964285cd8ed7d8de54d6247953e97c3')
install="${pkgname}.install"

pkgver() {
  local date="$(grep -Pom1 '(?<=<updated>).{4}-.{2}-.{2}(?=T.{2}:.{2}:.{2}Z</updated>)' feed.xml)"
  echo "${date//-/.}.${_commit:0:11}"
}

prepare() {
  sed -i 's#/etc/bash/bashrc.d/*#/usr/share/gentoo-bashrc/bashrc.d/*#' bashrc
}

package() {
  install -Dt "${pkgdir}/usr/share/gentoo-bashrc" -m755 bashrc
  install -Dt "${pkgdir}/usr/share/gentoo-bashrc/bashrc.d" -m755 10-gentoo-color.bash
  install -Dt "${pkgdir}/usr/share/gentoo-bashrc/bashrc.d" -m755 10-gentoo-title.bash
}
