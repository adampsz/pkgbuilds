pkgname=adwcustomizer-git
pkgver=r185.5229293
pkgrel=1
pkgdesc='Tool for customizing Libadwaita applications and the adw-gtk3 theme. '
arch=('x86_64')
url='https://github.com/ArtyIF/AdwCustomizer'
license=(MIT)
depends=('libadwaita-git>=1.2.alpha' libportal-gtk4 python python-anyascii libportal-gtk4)
makedepends=(git meson appstream-glib blueprint-compiler desktop-file-utils gobject-introspection)
optdepends=('adw-gtk3: The theme from libadwaita ported to GTK-3'
            'adw-gtk-theme: LibAdwaita Theme for all GTK3 and GTK4 Apps.')
provides=(adwcustomizer)
conflicts=(adwcustomizer)
source=("git+$url.git"
        'adwcustomizer.patch')
sha512sums=('SKIP'
            '205206810a5641a8ceb6760a6f72ef499c5cfc2ebbebfcd171893ea8820655e8f497ce68688f7451ff2adff8be05a8866ab1e498addcac69a77adcfd6f1aeee8')

pkgver() {
	cd "$srcdir/AdwCustomizer"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/AdwCustomizer"
	git apply ../../adwcustomizer.patch
}

build() {
	arch-meson "AdwCustomizer" build
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
