pkgname=jcloisterzone
pkgver=5.10.1
pkgrel=1
pkgdesc='Java implementation of Carcassonne board game.'
arch=('x86_64')
url="http://jcloisterzone.com/en/"
license=('AGPL')
depends=('java-runtime>=11')
makedepends=('yarn' 'libxcrypt-compat')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/farin/JCloisterZone-Client/archive/refs/tags/v${pkgver}.tar.gz"
        'JCloisterZone-Client.patch')
sha256sums=('fd1839513dbff5a9cb341d390d00ce132863fb1d162f9f53e21670b96373a438'
            '18697bee5a84e81768ef48b60c620ac85e71601541b4c810ebbf9fc67a8b6040')
options=('!strip')

prepare() {
    cd "${srcdir}/JCloisterZone-Client-${pkgver}"
    patch --forward --strip=1 --input="${srcdir}/JCloisterZone-Client.patch"
}

build() {
    cd "${srcdir}/JCloisterZone-Client-${pkgver}"

    yarn install --frozen-lockfile
    yarn run package

    # Extract data
    bsdtar -xv -C "./build" -f "./build/jcloisterzone-${pkgver}.deb"
}

package() {
    bsdtar -xv -C "${pkgdir}" -f "${srcdir}/JCloisterZone-Client-${pkgver}/build/data.tar.xz"
}
